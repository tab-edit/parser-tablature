@precedence { aggregate @left }

@top Tablature {
    Staves (newlineEmpty (newline|newlineEmpty)* Staves)* eof
}

Staves { 
    StaffLineGroup !aggregate (newline StaffLineGroup)*
}

StaffLineGroup { 
    StaffLine* (incomingNewline|incomingEOF)
}


StaffLine { 
    ( 
        (MeasureLineName (delim|dash)) | 
        delim
    )
    MeasureLine+ 
    (Multiplier | stafflineSep)
}

MeasureLine {
    (dash+ delim) |
    (dash* measureComponent+ delim)
}

// -----Measure components like fret, e.t.c.---------------
measureComponent { stringComponent }
// measureComponentGroup { stringComponentGroup }
stringComponent {
    Hammer |
    Pull |
    Slide |
    unitStringComponent dash*
}

Hammer[@isGroup=MeasureComponentGroup] { measureComponentConnector<hammerSym> }
Pull[@isGroup=MeasureComponentGroup] { measureComponentConnector<pullSym> }
Slide[@isGroup=MeasureComponentGroup] { measureComponentConnector<slideSym> }
measureComponentConnector<connector> {
    unitStringComponent dash*
    connector
    stringComponent
}

unitStringComponent {
    Fret | Harmonic | Grace
}
Fret[@isGroup=MeasureComponent] { fretNum }
Harmonic[@isGroup=MeasureComponent] { "[" Fret "]" }
Grace[@isGroup=MeasureComponent] { graceSym Fret }
// ---------------------------------------------------------
connectorSymb[@export] { slideSym|hammerSym|pullSym }

@skip { space | Comment }
@tokens {
    space { $[ \t] }
    dash { "-" }
    delimChar { $[|:] }
    num { $[0-9] }
    delim { delimChar delimChar? }

    Comment { "#" ![\n\r]* }
    
    fretNum { num num? }

    slideSym { $[s/\\] }
    hammerSym { $[hH] }
    pullSym { $[pP] }


    Multiplier { $[xX] num }
    MeasureLineName { $[eE] | $[bB] | $[gG] | $[dD] | $[aA] }
}

@external tokens newlines from "./tokens" { newline, newlineEmpty, eof }
@external tokens stafflineSeparator from "./tokens" { stafflineSep }
@external tokens lookaheads from "./tokens" { incomingNewline, incomingEOF }
@external tokens measureComponentExternTokens from "./tokens" { graceSym, incomingComponentConnector }